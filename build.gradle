buildscript {
	repositories {
		maven {
			url "http://maven.artifacts.furycloud.io/repository/all"
		}
	}

	dependencies {
		classpath group: 'com.mercadolibre', name: 'i18n', version: "$i18nVersion"
	}
}

plugins {
	id 'application'
	id 'java'
	id 'jacoco'
	id 'idea'
	id 'distribution'
	id 'it.gianluz.capsule' version '1.0.3'
	id 'com.monits.staticCodeAnalysis' version '2.6.4'
	id "io.swagger.core.v3.swagger-gradle-plugin" version "2.1.11"
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"
javadoc.options.encoding = "UTF-8"
jar.enabled = false

mainClassName = 'com.mercadolibre.Main'

group = 'com.mercadolibre'
version = '0.0.1-SNAPSHOT'

description = """px-checkout-mobile-payments"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

def envType = 'local'
if (project.hasProperty('production')) {
	envType = 'production'
}

sourceSets {
	main {
		resources {
			srcDir "src/main/resources/${envType}"
		}
	}
	test {
		resources {
			srcDir "src/main/resources/local"
		}
	}
}

repositories {
		maven { url "http://maven.artifacts.furycloud.io/repository/all" }
		mavenLocal()
	}

dependencies {
	compile "com.google.guava:guava:$guavaVersion"
	compile "com.google.code.gson:gson:$gsonVersion"
	compile "com.sparkjava:spark-core:$sparkVersion"
	compile "com.newrelic.agent.java:newrelic-api:$newRelicVersion"
	compile "commons-configuration:commons-configuration:$commonsConfigurationVersion"
	compile "com.mercadolibre.json:json-core:$jsonCoreVersion"
	compile "com.mercadolibre.px:px-toolkit:$pxToolkitVersion"
	compile "com.mercadolibre.px:px-dto-lib:$pxDtoLibVersion"
	compile "com.mercadolibre.px:px-api-lib:$pxApiLibVersion"
	compile "com.mercadolibre.px:px-monitoring-lib:$pxMonitoringLibVersion"
	implementation "com.mercadolibre:config-utils:${configsVersion}"

	//Melidata
	compile("com.mercadolibre.melidata:melidata-sdk-java:$melidataSdkVersion") {
		exclude group: "javax.servlet", module: "servlet-api"
	}

	// MLAuth for access_token filter info
	compile "com.mercadolibre.security:mlauth:$mlAuthVersion"

	// Apache
	compile 'org.apache.httpcomponents:httpcore:4.4.9'

	// Metrics and logs
	compile 'com.mercadolibre.metrics:datadog-metric-wrapper:0.0.11'

	// Lombok
	compileOnly 'org.projectlombok:lombok:1.18.10'
	annotationProcessor 'org.projectlombok:lombok:1.18.10'

	// Test tools
	testCompile 'junit:junit:4.13.2'
	testCompile "com.mercadolibre.restclient:meli-restclient-core:$restClientVersion:tests"
	testCompile "io.rest-assured:rest-assured:$restAssuredVersion"
	testCompile "org.mockito:mockito-core:$mockitoCoreVersion"
	testCompile 'org.jmockit:jmockit:1.49'
	testImplementation "org.mockito:mockito-inline:$mockitoCoreVersion"

	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

	// Swagger
	testCompile group: 'io.swagger.core.v3', name: 'swagger-core', version: '2.1.11'
	testCompile group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.1.1'

	// Babel
	compile ("com.mercadolibre:i18n:$i18nVersion") {
		exclude group: "org.apache.commons", module: "commons-io"
	}

	runtime files('lib/i18ngettext.jar') // i18n generated jar containing all the strings
}

defaultTasks 'run'

jacocoTestReport {
	reports {
		xml.enabled true
		html.enabled true
	}
	afterEvaluate {
		classDirectories = files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
					'com/mercadolibre/constants',
					'com/mercadolibre/Main.class',
					'com/mercadolibre/dto'
			])
		})
	}
}

test {
	// Endpoint tests run from within the ApiTest Suite, avoid double run
	exclude 'com/mercadolibre/endpoints/**'
	jvmArgs = [
			"-XX:+StartAttachListener",
			"-javaagent:${classpath.find { it.name.contains("jmockit") }.absolutePath}"
	]
	testLogging {
		exceptionFormat = 'full'
	}
	systemProperty("configFileName", "src/main/resources/configuration.properties")
	systemProperty("checksumEnabled", false)
}

check.dependsOn jacocoTestReport

task app(type: Jar) {
	archiveName = 'application.jar'
	manifest {
		attributes 'Main-Class': 'com.mercadolibre.Main'
	}
	from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
	with jar
}

task checkCodeFormat(type: Exec) {
	commandLine './bin/check_code_format.sh'
}

task wrapper(type: Wrapper) {
	gradleVersion = '4.10.3'
}

staticCodeAnalysis {
	ignoreErrors = true
}

plugins.withType(CheckstylePlugin) {
	checkstyle {
		maxWarnings = 0 // allow no warnings!
	}
}

distributions {
	main {
		baseName 'main'
		contents {
			from app
		}
	}
}

apply plugin: 'com.mercadolibre.i18n'

def buildJavadocFolder = "$buildDir/docs/javadoc"
def javadocFolder = "documentation/javadoc"

task deleteJavadoc(type: Delete) {
	group "documentation"
	delete javadocFolder
}

task createProjectJavadoc(type: Copy) {
	group "documentation"
	description 'Executes javadoc task and copies the result in the documentation folder.'

	dependsOn javadoc
	dependsOn deleteJavadoc

	from buildJavadocFolder
	into javadocFolder
}

task deleteJavadocBuildFolder(type: Delete) {
	group "documentation"
	delete buildJavadocFolder
}

createProjectJavadoc.finalizedBy(deleteJavadocBuildFolder)
test.finalizedBy(checkCodeFormat)

//Swagger configs begin
//Create a new source set to keep the Swagger resources outside main/test sources
sourceSets {
	doc {
		java {
			srcDir 'src/doc/java'
		}
		compileClasspath += sourceSets.test.runtimeClasspath
		runtimeClasspath += sourceSets.test.runtimeClasspath
	}
}

//Swagger Task Alias
task swagger {
	group "documentation"
	finalizedBy(resolve)
}

//Swagger Task Configuration
resolve {
	sortOutput = true
	outputFileName = 'swagger'
	outputFormat = 'YAML'
	prettyPrint = 'true'
	classpath = sourceSets.doc.runtimeClasspath
	resourcePackages = ['com.mercadolibre.swagger']
	outputDir = file('docs/specs/')
	readAllResources = false
	modelConverterClasses = ['com.mercadolibre.px.toolkit.swagger.SnakeCaseFieldModelConverter']
}

i18n {
	app = "px-checkout-mobile-payments"
	project = "mp-apps-px"
}